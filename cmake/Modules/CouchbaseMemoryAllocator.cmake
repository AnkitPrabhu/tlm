SET(COUCHBASE_MEMORY_ALLOCATOR "" CACHE STRING "The memory allocator to use. ")
SET_PROPERTY(CACHE COUCHBASE_MEMORY_ALLOCATOR PROPERTY STRINGS
             system jemalloc)

INCLUDE(FindCouchbaseJemalloc)

IF (COUCHBASE_MEMORY_ALLOCATOR)
   IF ("${COUCHBASE_MEMORY_ALLOCATOR}" STREQUAL "jemalloc")
      IF (JEMALLOC_FOUND)
         SET(MEMORY_ALLOCATOR jemalloc)
      ELSE (JEMALLOC_FOUND)
         MESSAGE(FATAL_ERROR "Could not find JEMalloc")
      ENDIF (JEMALLOC_FOUND)
   ELSEIF ("${COUCHBASE_MEMORY_ALLOCATOR}" STREQUAL "badmalloc")
      SET(MEMORY_ALLOCATOR badmalloc)
   ELSEIF ("${COUCHBASE_MEMORY_ALLOCATOR}" STREQUAL "system")
      SET(MEMORY_ALLOCATOR system)
   ELSE ("${COUCHBASE_MEMORY_ALLOCATOR}" STREQUAL "system")
      MESSAGE(FATAL_ERROR "Unknown memory allocator specified")
   ENDIF ("${COUCHBASE_MEMORY_ALLOCATOR}" STREQUAL "jemalloc")

ELSE (COUCHBASE_MEMORY_ALLOCATOR)
   # No allocator explicitly selected by user, prefer jemalloc if available
   IF (JEMALLOC_FOUND)
     SET(MEMORY_ALLOCATOR jemalloc)
   ELSE ()
     SET(MEMORY_ALLOCATOR system)
   ENDIF ()
ENDIF (COUCHBASE_MEMORY_ALLOCATOR)

# Finally, set the appropriate defines for our selected memory allocator.
IF (MEMORY_ALLOCATOR STREQUAL "jemalloc")
    ADD_DEFINITIONS(-DHAVE_JEMALLOC)
    SET(MALLOC_LIBRARIES ${JEMALLOC_LIBRARIES})
    SET(MALLOC_INCLUDE_DIR ${JEMALLOC_INCLUDE_DIR})
    MESSAGE(STATUS "Using JEMalloc")
ELSEIF (MEMORY_ALLOCATOR STREQUAL "system")
    ADD_DEFINITIONS(-DHAVE_SYSTEM_MALLOC)
    MESSAGE(STATUS "Using system-supplied malloc")
ENDIF (MEMORY_ALLOCATOR STREQUAL "jemalloc")
